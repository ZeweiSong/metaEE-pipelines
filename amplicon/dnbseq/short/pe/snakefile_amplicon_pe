#############################################
# Amplicon sequencing processing pipeline   #
# Used for Pair End reads                   #
# Burst for closed-reference clustering     #
# Winner take all for combining two targets #
#############################################

# This is a Snakemake pipeline for processing amplicon sequencing data based on 
# BGISEQ pair-end method. The insert library is build without controling the sequencing
# direction. So Read1 can start from either the forward primer or reverse primer, 
# randomly and in equal number.

# If you are not familiar with amplicon sequencing, or you have only done amplicon
# sequencing on illumina's platform, I would suggest you to read the following
# instructions carefully (at least once). Most of the illumina based protocol controls
# the direction of amplicons, so Read1 always occurs from the forward primers.
# This is NOT the case for our BGISEQ based protocol, and will result in some differences
# in data processing (and some intersting aftermath).

# Two types of amplicons appears in the PE data:
	# TYPE 1: Read1-forward primer-insert-reverse primer-Read2
	# TYPE 2: Read1-reverse primer-insert-forward primer-Read2
# For PE data, there is much less differnce than that in SE data, but we still distiguish
# these two types since they may different slightly on the reading position.
# For the commands in this pipeline, we refer read1 and read2 as r1 and r2,
# and refer the forward and reverse primers as fw and rv.
# Most of these labels appear as suffixes.
	# These are the two amplicons:
	# r1fw - r2rv
	# r1rv - r2fw

# You will need the sequence of the forward and reverse primers (or the conservative region
# you like to trim off). These primers are different based on the target amplified.
# Change the primer sequences in config.yaml to the one you used (and ask around if you don't know).
# Also check if you have the right Burst database (bacteria or fungi?).
# Check cluster.yaml if you use qsub.

# Get the sample label from data/samples
# The sample file should be in the format [SampleName].[ReadDirection].fq.gz, i.e. sampleA.r1.fq.gz sampleA.r2.fq.gz
# You can change the file extension in the config file.
# Put all sample files in data/samples
# A sample should have two corresponding files.

configfile: 'config.yaml'

import fnmatch
import os
import sys
SAMPLES = {}
sampleNames = []
ext = config['extension'] # The file extension can be set in the config file. Sometimes people use .fq instead of .fq.gz or other suffix.
for file in os.listdir('data/samples/'): # List all files under the folder. For PE data, one samples correspond to two files.
	if fnmatch.fnmatch(file, '*'+ext):
		string = file.split('.')
		sampleNames.append(string[0])
sampleNames = tuple(set(sampleNames)) # Deduplicate sample names for PE data.
for names in sampleNames:
	SAMPLES[names] = []
for file in os.listdir('data/samples/'):
	if fnmatch.fnmatch(file, '*'+ext):
		string = file.split('.')
		SAMPLES[string[0]].append('data/samples/' + file)
for key in SAMPLES.keys():
	SAMPLES[key].sort()
if len(SAMPLES) == 0:
	print("ERROR: no file found under data/samples/")
	print("Please check the file extension in the config file.")
	print("Snakemake has terminated.")
	sys.exit("Snake did not make it orz.")
#else:
	#print('Found {0} samples in the data/samples folder.'.format(len(SAMPLES)))
	#print('Samples to be processed are: {0}'.format(' '.join(sorted(SAMPLES.keys()))))

# Target of this pipeline: all samples in one profile
rule target:
	input:
		'data/combined.biom',
		'data/combined.taxa.biom',
		'data/combined.taxa.tsv',
		'data/count.txt'
		
# Find the two directions in the PE data		
rule direction:
	input:
		lambda wildcards: SAMPLES[wildcards.sample]
	output:
		r1fw = 'data/split_reads/{sample}.r1fw.fq',
		r2rv = 'data/split_reads/{sample}.r2rv.fq',
		r1rv = 'data/split_reads/{sample}.r1rv.fq',
		r2fw = 'data/split_reads/{sample}.r2fw.fq'
	params:
		fw=config['primers']['fw'],
		rv=config['primers']['rv']
	log:
		'logs/split_reads/{sample}.log'
	threads: 2
	run:
		print('Found {0} samples in the data/samples folder.'.format(len(SAMPLES)))
		print('Samples to be processed are: {0}'.format(' '.join(sorted(SAMPLES.keys()))))
		base = {'A':'T','T':'A','C':'G','G':'C','R':'Y','Y':'R','K':'M','M':'K','S':'S','W':'W','B':'V','V':'B','D':'H','H':'D','N':'N'}
		fwrc = ''.join([base[i] for i in list({params.fw})[0][::-1]])
		rvrc = ''.join([base[i] for i in list({params.rv})[0][::-1]])
		shell('cutadapt {input[0]} {input[1]} -g {params.fw} -a rvrc -G {params.rv} -A fwrc -n 2 --discard-untrimmed -e 0.1 -m 100 -j {threads} -o {output.r1fw} -p {output.r2rv} > {log}'.replace('fwrc', fwrc).replace('rvrc', rvrc))
		shell('cutadapt {input[0]} {input[1]} -g {params.rv} -a fwrc -G {params.fw} -A rvrc -n 2 --discard-untrimmed -e 0.1 -m 100 -j {threads} -o {output.r1rv} -p {output.r2fw} >> {log}'.replace('rvrc', rvrc).replace('fwrc', fwrc))

# Remove low quality reads		
rule quality:
	input:
		r1fw = 'data/split_reads/{sample}.r1fw.fq',
		r2rv = 'data/split_reads/{sample}.r2rv.fq',
		r1rv = 'data/split_reads/{sample}.r1rv.fq',
		r2fw = 'data/split_reads/{sample}.r2fw.fq'
	output:
		r1fw_qc='data/qc_reads/{sample}.r1fw.qc.fq',
		r2rv_temp=temp('data/qc_reads/{sample}.r2rv.temp'),
		r2rv_qc='data/qc_reads/{sample}.r2rv.qc.fq',
		r1rv_temp=temp('data/qc_reads/{sample}.r1rv.temp'),
		r1rv_qc='data/qc_reads/{sample}.r1rv.qc.fq',
		r2fw_qc='data/qc_reads/{sample}.r2fw.qc.fq'
	log:
		'logs/qc_reads/{sample}.log'
	threads: 2
	run:
		shell('sickle pe -f {input.r1fw} -r {input.r2rv} -o {output.r1fw_qc} -p {output.r2rv_temp} -t sanger -s single.fq > {log}')
		shell('sickle pe -f {input.r1rv} -r {input.r2fw} -o {output.r1rv_temp} -p {output.r2fw_qc} -t sanger -s single.fq >> {log}')
		shell('seqtk seq -r {output.r2rv_temp} > {output.r2rv_qc}')
		shell('seqtk seq -r {output.r1rv_temp} > {output.r1rv_qc}')
		shell('rm -f single.fq')

# Trim off sequences not in database and convert to FASTA
rule trim_tail:
	input:
		r1fw_qc='data/qc_reads/{sample}.r1fw.qc.fq',
		r2rv_qc='data/qc_reads/{sample}.r2rv.qc.fq',
		r1rv_qc='data/qc_reads/{sample}.r1rv.qc.fq',
		r2fw_qc='data/qc_reads/{sample}.r2fw.qc.fq'
	output:
		r1fw_qc=temp('data/qc_reads/{sample}.r1fw.qc.fa'),
		r2rv_qc=temp('data/qc_reads/{sample}.r2rv.qc.fa'),
		r1rv_qc=temp('data/qc_reads/{sample}.r1rv.qc.fa'),
		r2fw_qc=temp('data/qc_reads/{sample}.r2fw.qc.fa')
	params:
		fw_trim = config['trim']['fw'],
		rv_trim = config['trim']['rv']
	run:
		shell('seqtk trimfq -b {params.fw_trim} {input.r1fw_qc} | seqtk seq -A -L 50 - > {output.r1fw_qc}')
		shell('seqtk trimfq -e {params.rv_trim} {input.r2rv_qc} | seqtk seq -A -L 50 - > {output.r2rv_qc}')
		shell('seqtk trimfq -e {params.rv_trim} {input.r1rv_qc} | seqtk seq -A -L 50 - > {output.r1rv_qc}')
		shell('seqtk trimfq -b {params.fw_trim} {input.r2fw_qc} | seqtk seq -A -L 50 - > {output.r2fw_qc}')
		
# Align reads to the database
rule align:
	input:
		r1fw_qc=temp('data/qc_reads/{sample}.r1fw.qc.fa'),
		r2rv_qc=temp('data/qc_reads/{sample}.r2rv.qc.fa'),
		r1rv_qc=temp('data/qc_reads/{sample}.r1rv.qc.fa'),
		r2fw_qc=temp('data/qc_reads/{sample}.r2fw.qc.fa')
	output:
		r1fw_aln='data/alignments/{sample}.r1fw.b6',
		r2rv_aln='data/alignments/{sample}.r2rv.b6',
		r1rv_aln='data/alignments/{sample}.r1rv.b6',
		r2fw_aln='data/alignments/{sample}.r2fw.b6'
	params:
		acx=config['database']['acx'],
		edx=config['database']['edx'],
		mode=config['align']['mode'],
		id=config['align']['id']
	log:
		'logs/alignments/{sample}.log'
	threads: 4
	run:
		shell('burst12 -q {input.r1fw_qc} -a {params.acx} -r {params.edx} -o {output.r1fw_aln} -i {params.id} -m {params.mode} -t {threads} > {log}')
		shell('burst12 -q {input.r2rv_qc} -a {params.acx} -r {params.edx} -o {output.r2rv_aln} -i {params.id} -m {params.mode} -t {threads} >> {log}')
		shell('burst12 -q {input.r1rv_qc} -a {params.acx} -r {params.edx} -o {output.r1rv_aln} -i {params.id} -m {params.mode} -t {threads} >> {log}')
		shell('burst12 -q {input.r2fw_qc} -a {params.acx} -r {params.edx} -o {output.r2fw_aln} -i {params.id} -m {params.mode} -t {threads} >> {log}')

# Keep the paired alignments
rule keep_paired:
	input:
		r1fw_aln='data/alignments/{sample}.r1fw.b6',
		r2rv_aln='data/alignments/{sample}.r2rv.b6',
		r1rv_aln='data/alignments/{sample}.r1rv.b6',
		r2fw_aln='data/alignments/{sample}.r2fw.b6'
	output:
		fw_aln='data/alignments/{sample}.fw.b6',
		rv_aln='data/alignments/{sample}.rv.b6'
	log:
		'logs/keep_paired/{sample}.log'
	run:
		shell('amplicon_keepPairAln.py -i {input.r1fw_aln},{input.r2rv_aln} -o {output.fw_aln} > {log}')
		shell('amplicon_keepPairAln.py -i {input.r1rv_aln},{input.r2fw_aln} -o {output.rv_aln} >> {log}')

# Count reads retention in the above steps
rule count:
	input:
		direction_fw	=	'data/split_reads/{sample}.r1fw.fq',
		direction_rv	=	'data/split_reads/{sample}.r1rv.fq',
		quality_fw		=	'data/qc_reads/{sample}.r1fw.qc.fa',
		quality_rv		=	'data/qc_reads/{sample}.r1rv.qc.fa',
		align_fw		=	'data/alignments/{sample}.fw.b6',
		align_rv		=	'data/alignments/{sample}.rv.b6'
	output: 'data/count/{sample}.count'
	params:
		sn='{sample}'
	run:
		shell('echo {params.sn} > {output}')
		shell('echo -e "forward" >> {output}')
		shell('amplicon_countRetention.py -d {input.direction_fw} -q {input.quality_fw} -a {input.align_fw} >> {output}')
		shell('echo -e "reverse" >> {output}')
		shell('amplicon_countRetention.py -d {input.direction_rv} -q {input.quality_rv} -a {input.align_rv} >> {output}')

# Combine alignments using winner take all method
rule winnerTakeAll:
	input:
		fw_aln='data/alignments/{sample}.fw.b6',
		rv_aln='data/alignments/{sample}.rv.b6'
	output:
		tsv='data/profiles/{sample}.tsv',
		biom='data/profiles/{sample}.biom'
	params:
		sampleName='{sample}'
	log:
		'logs/winnerTakeAll/{sample}.log'
	shell:
		'amplicon_winnerTakeAll.py -i {input.fw_aln} {input.rv_aln} -sn {params.sampleName} -t {output.tsv} -b {output.biom} -g > {log}'

# Concatenate all profiles into one
# Add the taxonomy, and write to biom and tsv file	
rule concat:
	input:
		biom = expand('data/profiles/{sample}.biom', sample=SAMPLES),
		count = expand('data/count/{sample}.count', sample=SAMPLES)
	output:
		combined = 'data/combined.biom',
		biom_taxa = 'data/combined.taxa.biom',
		tsv_taxa = 'data/combined.taxa.tsv',
		count = 'data/count.txt'
	params:
		taxa=config['database']['tax']
	log:
		'logs/concat/concate.log'
	run:
		shell('amplicon_concat.py -i {input.biom} -biom_out {output.combined} > {log}')
		shell('biom add-metadata -i {output.combined} -o {output.biom_taxa} --observation-metadata-fp {params.taxa} --observation-header OTUID,taxonomy --output-as-json --sc-separated taxonomy')
		shell('biom convert -i {output.biom_taxa} -o {output.tsv_taxa} --to-tsv --header-key taxonomy')
		shell('cat {input.count} > {output.count}')